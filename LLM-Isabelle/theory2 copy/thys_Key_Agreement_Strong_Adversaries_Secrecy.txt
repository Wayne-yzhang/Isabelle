theory Secrecy 
imports Refinement IK
begin

declare domIff [simp, iff del] 



record s0_state = ik_state + 
  secret :: "msg set"



definition
  s0_secrecy :: "'a s0_state_scheme set"
where
  "s0_secrecy \<equiv> {s. synth (analz (ik s)) \<inter> secret s = {}}"

lemmas s0_secrecyI = s0_secrecy_def [THEN setc_def_to_intro, rule_format]
lemmas s0_secrecyE [elim] = s0_secrecy_def [THEN setc_def_to_elim, rule_format]



definition
  s0_add_secret :: "msg \<Rightarrow> ('a s0_state_scheme * 'a s0_state_scheme) set"
where
  "s0_add_secret m \<equiv> {(s,s').
    m \<notin> synth (analz (ik s)) \<and>
    
    s' = s\<lparr>secret := insert m (secret s)\<rparr>
  }"

definition
  s0_learn :: "msg \<Rightarrow> ('a s0_state_scheme * 'a s0_state_scheme) set"
where
  "s0_learn m \<equiv> {(s,s').
    s\<lparr>ik := insert m (ik s)\<rparr> \<in> s0_secrecy \<and> 

    s' = s\<lparr>ik := insert m (ik s)\<rparr>
  }"

definition
  s0_learn' :: "msg \<Rightarrow> ('a s0_state_scheme * 'a s0_state_scheme) set"
where
  "s0_learn' m \<equiv> {(s,s').
    synth (analz (insert m (ik s))) \<inter> secret s = {} \<and> 

    s' = s\<lparr>ik := insert m (ik s)\<rparr>
  }"


definition
  s0_trans :: "('a s0_state_scheme * 'a s0_state_scheme) set"
where
  "s0_trans \<equiv> (\<Union>m. s0_add_secret m) \<union> (\<Union>m. s0_learn m) \<union> Id"



definition
  s0_init :: "'a s0_state_scheme set"
where
  "s0_init \<equiv> s0_secrecy"

type_synonym
  s0_obs = "s0_state"

definition 
  s0 :: "(s0_state, s0_obs) spec" where
  "s0 \<equiv> \<lparr>
    init = s0_init,
    trans = s0_trans,
    obs = id
  \<rparr>"

lemmas s0_defs = s0_def s0_init_def s0_trans_def s0_add_secret_def s0_learn_def 
lemmas s0_all_defs = s0_defs ik_trans_defs

lemma s0_obs_id [simp]: "obs s0 = id"
by (simp add: s0_def)



lemma s0_secrecy_init [iff]: "init s0 \<subseteq> s0_secrecy"
by (simp add: s0_defs)

lemma s0_secrecy_trans [simp, intro]: "{s0_secrecy} trans s0 {> s0_secrecy}"
apply (auto simp add: s0_all_defs PO_hoare_defs intro!: s0_secrecyI)
apply (auto)
done

lemma s0_secrecy [iff]:"reach s0 \<subseteq> s0_secrecy"
by (rule inv_rule_basic, auto)


lemma s0_obs_secrecy [iff]:"oreach s0 \<subseteq> s0_secrecy"
by (rule external_from_internal_invariant) (auto del: subsetI)


lemma s0_anyP_observable [iff]: "observable (obs s0) P"
by (auto)


end


